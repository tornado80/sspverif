package KEM {
    params {
        b: Bool,
    }

    state {
        pk: Maybe(Bits(100)),
        sk: Maybe(Bits(1000)),
        ek: Maybe(Bits(400))
    }

    import oracles {
        GEN(),
        SET(k: Bits(256)),
        KEM_GEN() -> (Bits(100), Bits(1000)),
        KEM_ENCAPS(pk: Bits(100)) -> (Bits(256), Bits(400)),
        KEM_DECAPS(sk: Bits(1000), ek: Bits(400)) -> Bits(256),
    }

    oracle KEMGEN() -> Bits(100) {
        assert (sk == None);
        pk_sk <- invoke KEM_GEN();
        (pk1, sk1) <- parse pk_sk;
        pk <- Some(pk1);
        sk <- Some(sk1);
        return pk1;
    }

    oracle ENCAPS() -> Bits(400) {
        assert (pk != None as Bits(100));
        assert (ek == None);
        k_ek <- invoke KEM_ENCAPS(Unwrap(pk));
        (k, ek1) <- parse k_ek;
        ek <- Some(ek1);
        if b {
            _ <- invoke GEN();
        } else {
            _ <- invoke SET(k);
        }
        return ek1;
    }

    oracle DECAPS(encapsk: Bits(400)) -> Bits(256) {
        assert (sk != None as Bits(1000));
        assert (encapsk != Unwrap(ek));
        k <- invoke KEM_DECAPS(Unwrap(sk), encapsk);
        return k;
    }
}