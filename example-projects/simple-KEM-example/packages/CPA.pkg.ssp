package CPA {
    params {
    isideal: Bool,                                              /* real-ideal-bit        */
      kgen: fn Bits(256)             ->   Bits(256),            /* takes sk, returns pk  */
    encaps: fn (Bits(256),Bits(256)) ->  (Bits(256),Bits(256)), /* (pk,rand) -> (ctxt,k) */
    decaps: fn (Bits(256),Bits(256)) ->   Bits(256),            /* (sk,ctxt) -> k        */
    }
    
    state {
          sk:          Maybe(Bits(256)),             /* long-term sk             */
          pk:          Bits(256),                    /* long-term pk             */
          CTXT:        Table(Integer,Bits(256)),     /* sent ciphertexts         */
          KEY:         Table(Integer,Bits(256)),     /* keys                     */
          TESTED:      Table(Integer,Bool),          /* has the key been tested  */
          ctr:         Integer                       /* counter                  */
          }

oracle GetPK() -> Bits(256)                          /* returns pk                */
    {
      if (sk == None)                                /* sample sk if it has not been sampled yet */ 
      {
        sk_ <-$Bits(256);
        sk  <- Some(sk_);
        pk  <- kgen(sk_);
      }
      return pk;
    }

oracle ENC() -> Bits(256)                            /* samples rand and returns ctxt */
    {
      if (sk == None)                                /* sample sk if it has not been sampled yet */ 
      {
        sk_ <-$Bits(256);
        sk  <- Some(sk_);
        pk  <- kgen(sk_);
      }
      rand <-$ Bits(256);                          /* sample randomness              */
      (ctxt,k)  <- parse encaps(pk,rand);          /* encapsulate key                */
      CTXT[ctr] <- Some(ctxt);                     /* store ctxt to check that there is a ciphertext in Test  */
      KEY[ctr]  <- Some(k);                        /* store key                      */
      ctr <- (ctr + 1);                            /* increase counter               */
      return ctxt;
    }

    oracle Test(ctxt_id: Integer) -> Bits(256)  {       /* allows to get key or random key        */
        assert (CTXT[ctxt_id]   !=  None as Bits(256)); /* only allow testing for counter values which have been used */
        assert (TESTED[ctxt_id] !=  Some(true));        /* disallow testing of same counter twice */
        TESTED[ctxt_id] <- Some(true);                  /* mark id as tested                      */

        k <- Unwrap(KEY[ctxt_id]);                      /* retrieve key                           */

        if isideal { 
             k <-$ Bits(256);                      /* in ideal game, replace k by random     */
        }
        return k;                                  /* return key                             */
   }
}